plugins {
    id 'java'
    id 'application'
    id 'maven-publish'
    id 'signing'
    id 'org.graalvm.buildtools.native' version '0.10.6'
}

ext.isSnapshotVersion = version.endsWith("SNAPSHOT")
ext.isReleaseVersion = !ext.isSnapshotVersion

java {
    sourceCompatibility = '11'
    targetCompatibility = '11'
}

repositories {
    mavenCentral()
}

dependencies {
    testImplementation 'junit:junit:4.13.2'         
}

mainClassName = 'com.webcodepro.shrinkit.NufxScan' 

jar {
  manifest {
    attributes(
      'Main-Class': mainClassName,
      'Implementation-Title': 'ShrinkItArchive',
      'Implementation-Version': archiveVersion
    )
  }
}


javadoc {
    title = "ShrinkItArchive ${project.version}"
    source = sourceSets.main.allJava
    options.addStringOption('Xdoclint:none', '-quiet')
}

tasks.register('javadocJar', Jar) {
    archiveClassifier = 'javadoc'
    from javadoc
}

tasks.register('sourcesJar', Jar) {
    archiveClassifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar
            pom {
                groupId = "net.sf.applecommander"
                artifactId = "ShrinkItArchive"
                name = 'ShrinkItArchive'
                description = 'A Java library for managing Apple II ShrinkIt archives.'
                url = 'https://applecommander.github.io/'
                licenses {
                    license {
                        name = 'The GNU General Public License (GPL) Version 2, June 1991'
                        url = 'https://www.gnu.org/licenses/gpl-2.0.html'
                    }
                }
                developers {
                    developer {
                        id = 'robgreene'
                        name = 'Rob Greene'
                        email = 'robgreene@gmail.com'
                    }
                }
                scm {
                    connection = 'scm:git:https://github.com/AppleCommander/ShrinkItArchive.git'
                    developerConnection = 'scm:git:git@github.com:AppleCommander/ShrinkItArchive.git'
                    url = 'https://github.com/AppleCommander/applesingle'
                }
            }
            repositories {
                maven {
                    def releasesRepoUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2"
                    def snapshotsRepoUrl = "https://oss.sonatype.org/content/repositories/snapshots/"
                    url = isSnapshotVersion ? snapshotsRepoUrl : releasesRepoUrl
                    credentials {
                        username = findProperty('ossrhUsername')
                        password = findProperty('ossrhPassword')
                    }
                }
            }
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}

graalvmNative {
    String osName = System.getProperty('os.name').toLowerCase().split()[0]
    String osArch = System.getProperty('os.arch').toLowerCase().split()[0]
    binaries {
        named("main") {
            imageName.set("NufxScan-${osName}-${osArch}-${project.version}")
        }
    }
}
